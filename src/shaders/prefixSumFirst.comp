#version 430 core

layout (local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;

layout (binding = 1) coherent readonly buffer block1
{
    uint input_data[gl_WorkGroupSize.x];
};

layout (binding = 2) coherent writeonly buffer block2
{
    uint output_data[gl_WorkGroupSize.x];
};

shared uint shared_data[gl_WorkGroupSize.x * 2];

void main(void)
{
    uint id = gl_LocalInvocationID.x;
    uint z_id = gl_GlobalInvocationID.z * 2048;
    uint rd_id;
    uint wr_id;
    uint mask;

    const uint steps = uint(log2(gl_WorkGroupSize.x)) + 1;
    uint step = 0;

    shared_data[id * 2] = input_data[z_id + id * 2]  & 0x7;
    shared_data[id * 2 + 1] = input_data[z_id + id * 2 + 1] & 0x7;

    barrier();

    for (step = 0; step < steps; step++)
    {
        mask = (1 << step) - 1;
        rd_id = ((id >> step) << (step + 1)) + mask;
        wr_id = rd_id + 1 + (id & mask);

        shared_data[wr_id] += shared_data[rd_id];

        barrier();
    }

    output_data[z_id + id * 2] = shared_data[id * 2];
    output_data[z_id + id * 2 + 1] = shared_data[id * 2 + 1];
}